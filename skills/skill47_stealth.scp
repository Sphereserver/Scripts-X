//****************************************************************************
// SphereServer by: SphereServer development team and Menasoft.
// www.sphereserver.net
//****************************************************************************
VERSION=X1

[SKILL 47]
DEFNAME=Skill_Stealth
KEY=Stealth
TITLE=Rogue
DELAY=2.0
STAT_STR=20
STAT_DEX=90
STAT_INT=60
BONUS_STR=0
BONUS_DEX=80
BONUS_INT=20
BONUS_STATS=25
ADV_RATE=10.0,200.0,800.0

ON=@PreStart
	IF !(<FLAGS> & statf_invisible|statf_hidden)
		SYSMESSAGE You must hide first
		return 1
	ELIF (<HIDING> < 80.0)
		SYSMESSAGE You are not hidden well enough.  Become better at hiding.
		REVEAL
		return 1
	ELIF (<GetCharArmorRating> >= 42)
		SYSMESSAGE You could not hope to move quietly wearing this much armor.
		REVEAL
		return 1
	ENDIF

ON=@Wait
if <def.scp.NewSkills_Stealth>
f_start_stealth
f_skill_delay <getargv 0 <def.skill.47.waittime>>
return 1
endif

on=@select
f_skill_delay <getargv 0 <def.skill.47.waittime>>

on=@success
f_skill_delay <getargv 1 <def.skill.47.waittime>>

ON=@Fail
SRC.SYSMESSAGE You can't seem to hide here.
f_skill_delay <getargv 0 <def.skill.47.waittime>>

ON=@Abort
SRC.SYSMESSAGE You give up trying to hide here.

[function f_start_stealth]
if !<flags>&statf_hidden
sysmessage <def.scp.stealth_nothidden>
f_reveal
f_skill_delay <getargv 2 <def.skill.47.waittime>>
elseif <hiding> < <def.scp.stealth_hidingreq>
sysmessage <def.scp.stealth_reqhiding>
f_reveal
f_skill_delay <getargv 2 <def.skill.47.waittime>>
else
	if <f_armor_rating> >= 42
	sysmessage <def.scp.stealth_armorcheck>
	f_reveal
	f_skill_delay <getargv 2 <def.skill.47.waittime>>
	else
		if <r1,1200> > <max 150,<stealth>> // we might need a better success chance formula
		sysmessage <def.scp.stealth_fail>
		skillgain stealth <eval (<stealth> / 7) + 1>
		f_reveal
		f_skill_delay <getargv 2 <def.skill.47.waittime>>
		else
		sysmessage <def.scp.stealth_success>
		skillgain stealth <min 100,<eval (<stealth> / 10)>>
		f_stealth
		f_skill_delay <getargv 1 <def.skill.47.waittime>>
		endif
	endif
endif

[function f_stealth]
addbuff 1012,1075655,1075656,0,0
tag.stepstealth = <eval (<stealth> / 5.0)>
events +e_skill_stealth
flags |= statf_hidden
dspeech +spk_hidden
update

[events e_skill_stealth]
on=@stepstealth
if <flags>&statf_onhorse
f_reveal
endif
tag0.stepstealth -= <qval (<flags>&statf_fly) ? 2:1>
if <tag0.stepstealth> <= 0
	if !<flags>&statf_fly
	f_start_stealth
	else
	f_reveal
	endif
endif
argn1 = 0

on=@skillstart
if !((<argn1> == detectinghidden) || (<argn1> == itemid) || (<argn1> == anatomy) || (<argn1> == armslore) || (<argn1> == animallore) || (<argn1> == evaluatingintel) || (<argn1> == forensics) || (<argn1> == poisoning))
f_reveal
endif

on=@skillwait
if !((<argn1> == detectinghidden) || (<argn1> == itemid) || (<argn1> == anatomy) || (<argn1> == armslore) || (<argn1> == animallore) || (<argn1> == evaluatingintel) || (<argn1> == forensics) || (<argn1> == poisoning))
f_reveal
endif

on=@personalspace
src.sysmessage <def.scp.hiding_shoveinvis>
return 1

on=@spelleffect
if <argn1> == s_reveal
local.divisor = <eval (<hiding> + <stealth>)>
	if <local.divisor> > 0
	local.chance = <eval ((50 * (<src.magery> + <src.detectinghidden>)) / <local.divisor>)>
	else
	local.chance = 1000
	endif
	if <local.chance> > <r1000>
	f_reveal
	else
	return 1
	endif
endif

[function f_armor_rating]
for 3 24
	local.penaltytotal += <findlayer(<dlocal._for>).tag0.penalty.stealth>
endfor
return <dlocal.penaltytotal>

[FUNCTION GetCharArmorRating]
ARGS=4,6,7,10,13,19,24
WHILE (<dARGS>)
 IF !(<FINDLAYER.<ARGV0>.MageArmor>)
  LOCAL.AR += <DEF0.ArmorRating.<FINDLAYER.<ARGV0>.GetArmorMaterial>.Layer<ARGV0>>
 ENDIF
 ARGS=<STREAT <ARGS>>
ENDWHILE
return <LOCAL.AR>

[FUNCTION GetArmorMaterial]
IF (<DISPIDDEC> >= 5061) && (<DISPIDDEC> <= 5075)
 return Leather
ELIF (<DISPIDDEC> >= 5076) && (<DISPIDDEC> <= 5090)
 return Studded
ELIF (<DISPIDDEC> >= 5198) && (<DISPIDDEC> <= 5207)
 return Bone
ELIF (<DISPIDDEC> >= 5093) && (<DISPIDDEC> <= 5106)
 return Ring
ELIF (<DISPIDDEC> >= 5051) && (<DISPIDDEC> <= 5060)
 return Chain
ELIF (<DISPIDDEC> >= 5128) && (<DISPIDDEC> <= 5146)
 return Plate
ELIF (<DISPIDDEC> >= 9793) && (<DISPIDDEC> <= 9800)
 return Dragon
ENDIF

[DEFNAME ArmorRating]
ArmorRating.Studded.Layer4	6
ArmorRating.Studded.Layer7	2
ArmorRating.Studded.Layer10	2
ArmorRating.Studded.Layer13	10
ArmorRating.Studded.Layer19	4
ArmorRating.Bone.Layer4		15
ArmorRating.Bone.Layer6		10
ArmorRating.Bone.Layer7		5
ArmorRating.Bone.Layer13	25
ArmorRating.Bone.Layer19	10
ArmorRating.Ring.Layer4		15
ArmorRating.Ring.Layer7		5
ArmorRating.Ring.Layer13	25
ArmorRating.Ring.Layer19	10
ArmorRating.Chain.Layer4	15
ArmorRating.Chain.Layer6	10
ArmorRating.Chain.Layer13	25
ArmorRating.Plate.Layer24	15
ArmorRating.Plate.Layer6	10
ArmorRating.Plate.Layer7	5
ArmorRating.Plate.Layer10	5
ArmorRating.Plate.Layer13	25
ArmorRating.Plate.Layer19	10
ArmorRating.Dragon.Layer4	15
ArmorRating.Dragon.Layer6	10
ArmorRating.Dragon.Layer7	5
ArmorRating.Dragon.Layer13	25
ArmorRating.Dragon.Layer19	10

[EOF]
