//****************************************************************************
// SphereServer by: SphereServer development team and Menasoft.
// www.sphereserver.net
//****************************************************************************
VERSION=X1

// SCRIPT CREDITS
// --------------
// Cloud_BR - All the script
//
// SCRIPT DOCUMENTATION
// --------------------
// OSI-Like SpiritSpeak

[SKILL 32]
DEFNAME=Skill_SpiritSpeak
KEY=SpiritSpeak
TITLE=Medium
DELAY=2.0
STAT_STR=20
STAT_DEX=30
STAT_INT=70
BONUS_STR=0
BONUS_DEX=0
BONUS_INT=100
BONUS_STATS=50
ADV_RATE=2.5,50.0,200.0

on=@wait
if <def.scp.NewSkills_SpiritSpeak> == 1
	if <hits> >= <maxhits>
	src.sysmessage <def.scp.spiritspeak_fullhealed>
	return 1
	elif <src.mana> <= 10
	src.sysmessage <def.scp.spiritspeak_nomana>
	return 1
	endif
endif

on=@select
f_skill_delay <getargv 0 <def.skill.32.waittime>>

on=@success
if <def.scp.NewSkills_SpiritSpeak> == 0
return 0
endif
f_skill_delay <getargv 1 <def.skill.32.waittime>>
skillgain spiritspeak <eval (<spiritspeak> / 10) + 1>
foritems 3
	if <type>==t_corpse
		if (<canseelos>)
			if !<morey>
			local.maxdamhealed=<eval <src.spiritspeak>/20>
			local.mindamhealed=<eval (<src.fame>/500)+1>
				if <local.mindamhealed> < <local.maxdamhealed>
				src.hits += <r<local.mindamhealed>,<eval <local.maxdamhealed>+1>>
				else
				src.hits += <local.maxdamhealed>
				endif
			src.sound 586
			src.say <def.scp.spiritspeak_wop>
			src.anim 17
			morey=1
			color=0349
				if <src.hits> > <src.maxhits>
				src.hits=<src.maxhits>
				endif
			src.sysmessage <def.scp.spiritspeak_healcorpse>
			src.effect 3,i_fx_bless_effect,1,20
			return 1
			endif
		endif
	endif
endfor
local.maxdamhealed=<eval (<src.spiritspeak>/100)+4>
local.mindamhealed=<eval (<local.maxdamhealed>/2)+1>
src.hits += <r<local.mindamhealed>,<eval <local.maxdamhealed>+1>>
	if <src.hits> > <src.maxhits>
	src.hits=<src.maxhits>
	endif
src.sysmessage <def.scp.spiritspeak_healself>
src.mana -= 10
src.effect 3,i_fx_bless_effect,1,20
return 1

on=@fail
sysmessage @,,2 502443
f_skill_delay <getargv 2 <def.skill.32.waittime>>

ON=@abort
sysmessage @,,2 502443
f_skill_delay <getargv 2 <def.skill.32.waittime>>

[EOF]