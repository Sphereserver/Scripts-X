//****************************************************************************
// SphereServer by: SphereServer development team and Menasoft.
// www.sphereserver.net
//****************************************************************************
VERSION=X1


[typedef t_ore]
on=@dclick
if <def0.scp.OSIOreFormula> < 1
   return 0
endif
if <cont> != <src.findlayer.21>
   src.sysmessage <def.scp.craft_smelt_notinpack>
elif <src.mining> >= <eval <skillmake.1.value>-100>
   if <src.isneartype t_forge 2>
      args = <src.isneartype.p t_forge 2>
      forclients 18
         src.f_fireforge
      endfor
      if <r> <= <eval (((<src.mining> - <skillmake.1.value>)*2)+50)>
         serv.newitem <tdata1>
         new.amount <eval ((<amount>*(<src.mining>/10))/100)>
         if <new.amount> < 1
            new.amount = 1
         endif
         src.sysmessage <def.scp.craft_smelt_success>
         src.consume <new.amount> <baseid>
         new.cont <src.findlayer.21>
      else
         src.sysmessage <def.scp.craft_smelt_fail>
         src.consume 1 <baseid>
      endif
   else
      src.sysmessage @,,2 <def.scp.craft_msg_smithsmeltneeds>
   endif
else
   src.sysmessage <def.scp.craft_smelt_noskill>
endif
return 1

[FUNCTION f_fireforge] //Make fire on the forge
FORITEMS 3
	IF (<TYPE> == t_forge) 
	   SERV.NEWITEM i_fire
	   NEW.ATTR=attr_move_never|attr_decay
	   NEW.P=<P>
	   NEW.NUDGEUP 8
	   NEW.TIMER=1
	ENDIF
ENDFOR


[eof]
