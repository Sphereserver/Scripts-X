//****************************************************************************
// SphereServer by: SphereServer development team and Menasoft.
// www.sphereserver.net
//****************************************************************************
VERSION=X1


[ITEMDEF i_house_placement_tool]
ID=014f6
NAME=House Placement Tool
VALUE=600
DUPELIST=014f5

ON=@DClick
	if (<topobj> != <src>)
		doswitch <src.f_LangShowNum>
			src.sysmessage @<def.fail>,,1 The item must be on your backpack.
			src.sysmessage @<def.fail>,,1 Предмет должен находится в вашем рюкзаке.
		enddo
		return 1
	endif
	sdialog d_house_placement_tool
	return 1


[ITEMDEF i_trashcan]
NAME=trashcan
ID=i_barrel_open
TYPE=t_container

CATEGORY=Special Items
SUBSECTION=Unequippable
DESCRIPTION=Trashcan

On=@Create
	COLOR=0835

On=@Dropon_Self
	src.sound = 011e
	argo.remove
	return 1


[ITEMDEF i_house_interior_decorator]
ID=0fc1
NAME=Interior Decorator
VALUE=10000

On=@ClientToolTip
	if (<TAG0.MODE>)
		addcliloc <eval 1018322+<TAG0.MODE>>
	endif

On=@Click
	if (<TAG0.MODE> == 1)
		message (turn)
		doswitch <src.f_LangShowNum>
			message (turn)
			message @,,1 (вращать)
		enddo
	elseif (<TAG0.MODE> == 2)
		doswitch <src.f_LangShowNum>
			message (up)
			message @,,1 (вверх)
		enddo
	elseif (<TAG0.MODE> == 3)
		doswitch <src.f_LangShowNum>
			message (down)
			message @,,1 (вниз)
		enddo
	endif

On=@DClick
	ref1 = <src.region.uid>
	if (<cont.topobj> != <src>)
		doswitch <src.f_LangShowNum>
			src.sysmessage @<def.fail>,,1 The item must be on your backpack.
			src.sysmessage @<def.fail>,,1 Предмет должен находится в вашем рюкзаке.
		enddo
		return 1
	elseif !((<ref1.isowner <src>>) || (<ref1.getcoownerpos <src>> >= 0) || (<src.isgm>))
		doswitch <src.f_LangShowNum>
			src.sysmessage @<def.fail>,,1 Only owner and co-owners can change items locations!
			src.sysmessage @<def.fail>,,1 Только собственник и совладельцы могут менять предметы.
		enddo
		return 1
	elseif !(<TAG0.MODE>)
		sdialog d_house_interior_decorator
	else
		TAG.House = <REGION.UID>
		TARGET
	endif
	return 1

On=@TargOn_Char
	TAG.House =
	return 1

On=@TargOn_Cancel
	TAG.House =

On=@TargOn_Item
	if (<argo> == <uid>)
		TAG.House =
		TAG.Mode =
		sdialog d_house_interior_decorator
		return 1
	else
		ref1 = <argo.region.uid>
		if (<ref1> != <TAG0.HOUSE>) || !(<src.f_house_isInside>)
			doswitch <src.f_LangShowNum>
				src.sysmessage @<def.fail>,,1 You should be located in the region of the house.
				src.sysmessage @<def.fail>,,1 Вы должны находится на территории дома.
			enddo
			return 1
		elseif (<argo.cont>) || (<argo.region.uid> != <TAG0.HOUSE>) || !(<argo.f_house_isInside>)
			doswitch <src.f_LangShowNum>
				src.sysmessage @<def.fail>,,1 The item should be located in the region of the house.
				src.sysmessage @<def.fail>,,1 Предмет должен находится на территории дома.
			enddo
			return 1
		elseif !(<argo.isevent.ei_house_lockdown>)
			doswitch <src.f_LangShowNum>
				src.sysmessage @<def.fail>,,1 The item should be locked.
				src.sysmessage @<def.fail>,,1 Предмет должен быть закреплен.
			enddo
			return 1
		elseif !(<ref1.owner> == <src>)
			doswitch <src.f_LangShowNum>
				src.sysmessage @<def.fail>,,1 The item should be located in the region of your house.
				src.sysmessage @<def.fail>,,1 Предмет должен находится на территории вашего дома.
			enddo
			return 1
		else
			TAG.House =
			if (<TAG0.Mode> == 1)
				if !(<argo.flip>)
					doswitch <src.f_LangShowNum>
						src.sysmessage @<def.fail>,,1 You can not flip it!
						src.sysmessage @<def.fail>,,1 Этот предмет невозможно повернуть.
					enddo
				else
					argo.flip
				endif
				return 1
			elseif (<TAG0.MODE> == 2)
				if (<eval(<argo.p.z>-<argo.tag.startedz>)> >= 15)
					doswitch <src.f_LangShowNum>
						src.sysmessage @<def.fail>,,1 This item can not go upper!
						src.sysmessage @<def.fail>,,1 Этот предмет невозможно поднять выше.
					enddo
				else
					argo.nudgeup
				endif
				return 1
			elseif (<TAG0.MODE> == 3)
				if (<eval(<argo.p.z>-<argo.tag.startedz>)> <= 0)
					doswitch <src.f_LangShowNum>
						src.sysmessage @<def.fail>,,1 This item can not go lower!
						src.sysmessage @<def.fail>,,1 Этот предмет невозможно опустить ниже.
					enddo
				else
					argo.nudgedown
				endif
				return 1
			endif
		endif
		return 1
	endif

[ITEMDEF i_house_transfer]
ID = i_memory
type = t_script

[EOF]