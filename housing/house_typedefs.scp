//****************************************************************************
// SphereServer by: SphereServer development team and Menasoft.
// www.sphereserver.net
//****************************************************************************
VERSION=X1

// House sign override
[TYPEDEF ei_house_sign]
ON=@Create
timerf 1, trigger @HouseSysInit // The LINK is automatically set immediately after placement and @Create

ON=@HouseSysInit
if !<link.tag0.convert>
    attr=010
    timer = -1
else
    if <def.hs_can_decay>
        attr=02|010
        timer=60*60*24*<def.hs_can_decay>    
    else
        attr=010
		timer =-1
    endif
endif
link.REGION.EVENTS=+r_house_system


ON=@Click
if !<link.region.tag0.forsale>
    if <link.housetype = 2>
        message <name>
        message [<uid.<tag0.is_guild>.abbrev>]
        return 1
    else
        return 0
    endif
else
    message For Sale
    message <link.dtag0.price> gp
    return 1
endif    


ON=@ClientToolTip
if (<link>==<def.link_invalid>)
    return 0
endif
if <link.tag0.demolition>
    addcliloc 1070722, Demolition Scheduled in <QVAL (<eval <timer>/86400> < 1)? <eval (<timer>%86400)/3600> hours and <eval ((<timer>%86400)%3600)/60> minutes.:<eval <timer>/86400> days>
elif <link.region.tag0.forsale>
    addcliloc 1070722, FOR SALE <link.dtag0.price>gp
endif
addcliloc 1061639,<name>
addcliloc 1061640,<uid.<more1>.name>
if <link.housetype>==<def.house_guild>
    addcliloc 1042971,[<uid.<tag0.is_guild>.abbrev>] Guild House
endif
addcliloc <QVAL (<link.housetype>==<def.house_private>)? 1061642:1061641>
return 1


ON=@DClick
if (<link>!=<def.link_invalid>)
    if !(<def.hs_can_decay>)
        if (<attr>&02)
            attr &=~ 02
            timer -1
        endif
    else
        if !(<attr>&02)
            attr |= 02
            if <timer> < 0
                timer=60*60*24*<def.hs_can_decay>
            endif
        endif
    endif
endif
if (<link.isowner <src>>) || (<link.GetCoownerPos <src>> >= 0) || (<link.GetFriendPos <src>> >= 0) || (<src.isgm>)
    if (<src.region.uid>==<link>)
        link.dialog d_house_menu 
        return 1        
    else
        src.sysmessage @32,,1 You must be standing on your doorstep to access the house menu.
        return 1
    endif
else
    if !<link.region.tag0.forsale>
        link.dialog d_house_visitor
        return 1
    else
        link.dialog d_house_forsale
        return 1
    endif
endif


ON=@Timer
ref2=<link.owner>
if (<def.hs_can_decay>)
    attr 012
    if (<link>)
        link.redeed
    endif
    if <ref2.isonline>
        ref2.sysmessage @,,1 Your <link.name> in <region.region.name> has collapsed
        ref2.sysmessage @,,1 The number of houses you own is now <ref2.houses>
    endif
else
    return 1
endif


// Event for locked down items.
[TYPEDEF ei_house_lockdown]
ON=@ClientToolTip
addcliloc 501643
 
ON=@PickUp_Ground   // Do not allow to move this item
if (!<src.IsGM>)
    return 1
endif
 
ON=@PickUp_Self // For containers only, do not allow to pick up items
if (!<src.IsGM>)
    return 1
endif
 
// When entering customize mode, all locked down items and containers are being moved to the Moving Crate
// but they are still Locked Down items, counting as so and having their properties
// so they can't be moved from ground nor items can be dragged from them
// but they can still be picked up from the moving crate and dropped on the ground or other containers
// that's why some checks must be done to prevent malintentionated/unintended placements outside the multi.
ON=@DropOn_Ground
if (<region.uid> != <link.region.uid>)
    return 1
endif
 
ON=@DropOn_Item
if (<argo.region.uid> != <link.region.uid>)
    return 1
endif
 
// Doors event 
[TYPEDEF ei_house_door]
ON=@Click	
if (<tag0.guild>)
	message @,,1 [<uid.<tag0.guild>.abbrev>] Guild Members Only.
	message @,,1 <name>
	return 1
elif (<tag0.owneronly>)
	message @,,1 Owner Only
	message @,,1 <name>
	return 1
elif (<tag0.restricted>==1)
	message @,,1 Co-Owners Only
	message @,,1 <name>
	return 1
elif (<tag0.restricted>==2)
	message @,,1 Friends Only
	message @,,1 <name>
	return 1
endif

ON=@ClientToolTip
if (<tag0.guild>)
	addcliloc 1042971, [<uid.<tag0.guild>.abbrev>] Guild Members Only
	return 1
elif (<tag0.owneronly>)
	addcliloc 1070722, Private
	addcliloc 1042971, Owner Only
	return 1
elif (<tag0.restricted>)
	if (<tag.restricted>==1)
		addcliloc 1042971, Restricted Access
		addcliloc 1070722, Owner & Co-owners Only
		return 1
	elif (<tag.restricted>==2)
		addcliloc 1042971, Restricted Access
		addcliloc 1070722, Owner Co-owners & Friends Only
		return 1
	endif
endif

ON=@DClick
ref1=<region.uid>
if <def.hs_can_decay> 
	if (<ref1.tag0.decay_exempt>)
		ref1.link.timer=-1
	else
		if !<def.hs_property_tax>
			if (<ref1.isowner <src>>)
				if !(<ref1.tag0.demolition>)
					ref1.link.timer=60*60*24*<def.hs_can_decay>
				else
					src.sysmessage @32,,1 This house is improperly placed and has been scheduled for demolition in  <QVAL (<eval <ref1.link.timer>/86400> < 1)? <eval (<ref1.link.timer>%86400)/3600> hours and <eval ((<ref1.link.timer>%86400)%3600)/60> minutes.:<eval <ref1.link.timer>/86400> days.>
					src.sysmessage @32,,1 Unless you redeed it yourself before that time the house and all items within will be lost without refund.
				endif
			endif
		else
			if (<ref1.tag0.demolition>)
				src.sysmessage @32,,1 This house is improperly placed and has been scheduled for demolition in  <QVAL (<eval <ref1.link.timer>/86400> < 1)? <eval (<ref1.link.timer>%86400)/3600> hours and <eval ((<ref1.link.timer>%86400)%3600)/60> minutes.:<eval <ref1.link.timer>/86400> days.>
				src.sysmessage @32,,1 Unless you redeed it yourself before that time the house and all items within will be lost without refund.
			endif
		endif
	endif
endif
if (<serv.AutoHouseKeys>)   // If keys are used, let the server check them.
    return 0
endif
if !(<src.isgm>)
	if (<ref1.f_house_can_access> != <def.house_access_none>)
        UseDoor
        return 1
	endif
endif

// House container created from COMPONENTs from [MULTIDEF ] (this also have 'ei_house_component').
[TYPEDEF ei_house_container]
 
// House Component created from [MULTIDEF ].
[TYPEDEF ei_house_component]

// Event for Moving Crates.
// Moving Crates are meant for House Customizing purposes, some items are moved inside when required
// in customize mode. Player storage is NOT allowed.
// Moving Crates are automatically deleted when there are no items inside.
// *More behaviour on player side event (e_moving_crate).
[TYPEDEF ei_moving_crate]
if !<src.isgm>
	return 1
endif

ON=@PickUp_Self
if <count> <= 1
	remove
endif

ON=@destroy
ref1 = <link.owner>
if (<ref1> && <ref1.isevent.e_moving_crate>)
	ref1.events -e_moving_crate
endif

ON=@create
attr |= <def.attr_invis>

// Secured Containers.
// Only allowed players can manage their storage (Refer to f_house_can_access to see the allowance).
[TYPEDEF ei_house_secure]
ON=@clienttooltip
addcliloc 501644
 
ON=@PickUp_ground
if (!<src.IsGM>)
    return 1
endif

ON=@DClick
if (<f_house_can_access>)
    return 0
endif
return 1
 
ON=@PickUp_Self
if (<f_house_can_access>)
    return 0
endif
return 1

// Telepad for multis.
[typedef ei_house_telepad]
ON=@Click	
if (<tag0.guild>)
	message @,,1 [<uid.<tag0.guild>.abbrev>] Guild Members Only.
	message @,,1 <name>
	return 1
elif (<tag0.access>==house_access_owner)
	message @,,1 Owner Only
	message @,,1 <name>
	return 1
elif (<tag0.access>==house_access_coowner)
	message @,,1 Co-Owners Only
	message @,,1 <name>
	return 1
elif (<tag0.access>==house_access_friend)
	message @,,1 Friends Only
	message @,,1 <name>
	return 1
endif

ON=@ClientToolTip
if (<tag0.guild>)
	addcliloc 1042971, [<uid.<tag0.guild>.abbrev>] Guild Members Only
	return 1
elif (<tag0.access>==house_access_owner)
	addcliloc 1070722, Private
	addcliloc 1042971, Owner Only
	return 1
elif (<tag0.access>==house_access_coowner)
	addcliloc 1042971, Restricted Access
	addcliloc 1070722, Owner & Co-owners Only
	return 1
elif (<tag0.access>==house_access_friend)
	addcliloc 1042971, Restricted Access
	addcliloc 1070722, Owner Co-owners & Friends Only
	return 1
endif


ON=@step
if !(<link>==<def.link_invalid>) 
	if !(<src.isgm>)
		if (<f_house_can_access>)
			return 0
		endif
		return 1
	endif
endif

// @Custom event for house transfers
[TYPEDEF t_trade_house_deed]
ON=@Click
MESSAGE House Transfer Contract
MESSAGE House Name: <LINK.NAME>
MESSAGE Owner: <UID.<LINK.MORE1>.NAME>
MESSAGE Location: <LINK.SEXTANTP> //<LINK.REGION.REGION.NAME> (<LINK.P>)
return 1

ON=@ClientToolTip
addcliloc 1061112,<LINK.NAME>
addcliloc 1061113,<UID.<LINK.MORE2>.NAME>
addcliloc 1061114,<STRARG <LINK.SEXTANTP>> <STREAT <LINK.SEXTANTP>>

ON=@Timer
if (<CONT.TYPE>==t_eq_trade_window)
	TIMERF 2,trigger @Timer
else
	UID.<MORE1>.EVENTS=-e_house_transfer
	REMOVE
endif
return 1

ON=@HouseTraded
REF1=<MORE1>		//New Owner
REF2=<LINK.owner>	//Old Owner
REF1.EVENTS=-e_house_transfer
ref2.events=-e_house_transfer
LINK.TAG.lasttransfer=<serv.rtime>
ref2.delhouse <uid>
LINK.owner=<REF1>
for <eval <REF2.COUNT>-1> 0
	if (<REF2.FINDCONT.<LOCAL._FOR>.LINK>==<LINK.LINK>)
		REF2.FINDCONT.<dLOCAL._FOR>.CONT=<REF1>
	endif
endfor
REF2.SYSMESSAGE @,,1 You transfer the property to <ref1.name>
ref2.sysmessage @,,1 The number of houses you own is now <ref2.houses>.
REF1.SYSMESSAGE @,,1 <ref2.name> has transferred ownership of this property to you.
ref1.sysmessage @,,1 The number of houses you own is now <ref1.houses>.
link.delban -1
link.delcoowner -1
link.delaccess -1
link.delfriend -1
link.resendtooltip 1

// @Override of t_deed typedef
[TYPEDEF t_deed]
ON=@DClick
IF (<CONT> != <SRC.FINDLAYER.21>)
  SRC.SYSMESSAGE @,,1 The item must be on your backpack.
  return 1
ENDIF
IF (<DISPID>==i_gold)
 TIMERF 60,TRIGGER @TargOn_Cancel
ENDIF

ON=@TargOn_Ground
IF !(<SRC.ISGM>)
  IF !STRMATCH("*ship*","<BASEID>")
    IF (<SRC.TAG0.House.PlacementDelay> > <SERV.TIME>)
      SRC.SYSMESSAGE @,,1 Number of days until you can place another house: <eval (<SRC.TAG0.House.PlacementDelay>-<SERV.TIME>)/864000>
      return 1
    ELIF !(<DEF0.PLACEMENT_ILSHENAR_FACET>) && (<SRC.TARGP.M>==2)
      SRC.SYSMESSAGE @,,1 Housing cannot be created in this area.
      TRIGGER @TargOn_Cancel
      return 1
    ENDIF
  ENDIF
  SRC.TIMERF 1,f_house_isPlaced <MORE1> <BASEID> <SRC.TARGP>
ENDIF

ON=@TargOn_Char
SRC.SYSMESSAGE @,,1 You must place your house on ground.
TRIGGER @TargOn_Cancel
return 1

ON=@TargOn_Item
SRC.SYSMESSAGE @,,1 You must place your house on ground.
TRIGGER @TargOn_Cancel
return 1

ON=@TargOn_Cancel
IF (<DISPID>==i_gold)
 SRC.SDIALOG d_house_placement_tool
 REMOVE
ENDIF

// @Custom region event for houses.
[REGIONTYPE r_house_system]
ON=@Enter
if <src.isplayer>
	src.events +e_house_player_events
	SRC.dSPEECH +spk_player_house
	src.timerf 1, f_house_visitor_count
	ref1=<uid>
	if !<src.isgm>
		if <ref1.housetype>==house_private
			if (<ref1.isowner <src>>) || (<ref1.getcoownerpos <src>> >= 0) || (<ref1.getfriendpos <src>> >= 0) || (<src.isgm>) 
				return 0
			elif (<ref1.housetype>==house_guild)
				if (<src.guild>==<ref1.tag0.is_guild>)
					return 0
				else
					src.sysmessage @32,,1 You are not a member of this guild
					return 1
				endif
			else
				src.sysmessage @32,,1 This is private property.
				src.sysmessage @32,,1 You may not trespass.
				return 1
			endif
		else
			if <ref1.getbanpos <src>> >= 0
				src.sysmessage @32,,1 You are banned from this property
				return 1
			else
				return 0
			endif
		endif
	endif
endif

ON=@Exit	
	src.dialogclose d_house_menu 
	src.events -e_house_player_events	


//////////////////////////////////////////////////////////////////////////////////////events///////////////////////////////////////////////////
[EVENTS e_house_transfer]
ON=@TradeAccepted
DIALOGCLOSE d_house_demolish
DIALOGCLOSE d_house_menu
if <argn1>
	for <ARGN1>
		if <REF<dLOCAL._FOR>>
			if (<REF<dLOCAL._FOR>.TYPE>==t_trade_house_deed)
				UID.<REF<dLOCAL._FOR>>.TRIGGER @HouseTraded
				UID.<REF<dLOCAL._FOR>>.REMOVE
			endif
		endif
	endfor
endif
EVENTS=-e_house_transfer

[EVENTS e_house_player_events]
ON=@ItemDClick
if (<act.defname>==<def.vendor_deed_id>)
	ref1=<region.uid>
	if !<def.house_list_vendors>
		if <ref1.isowner <uid>> || (<src.isgm)
			if <ref1.housetype>==house_private
				src.sysmessage @32,,1 You must set your building to public before you can place vendors here.
				return 1
			else 
				if (<ref1.vendors> < <ref1.maxvendors>)
					return 0
				else
					sysmessage @32,,1 You have no available vendor slots
					sysmessage @32,,1 You must remove an existing vendor before you can place a new one.
					return 1
				endif
			endif
		else
			sysmessage @32,,1 Only the home owner may place vendors here.
			return 1
		endif
	else
		if (<ref1.isowner <src>>) || (<ref1.getcoownerpos <src>> >= 0) || (<ref1.getfriendpos <src>> >= 0) || (<src.isgm)
			if <ref1.housetype>==house_private
				src.sysmessage @32,,1 You must set your building to public before you can place vendors here.
				return 1
			else 		
				if (<ref1.vendors> < <ref1.maxvendors>)
					return 0
				else
					sysmessage @32,,1 You have no available vendor slots
					sysmessage @32,,1 You must remove an existing vendor before you can place a new one.
					return 1
				endif
			endif
		else
			sysmessage @32,,1 Only people on the home lists may place a vendor here.
			return 1
		endif
	endif
endif

ON=@ContextMenuRequest
ref1 = <region.uid>
if <uid>==<src> && <ref1.link>
	src.addcontextentry 101,3006207
endif

ON=@ContextMenuSelect
if <argn>==101
	ref1 = <region.uid>
	src.go <uid.<ref1.link>.p>
endif

[EVENTS e_house_customize]
ON=@SkillStart
	IF !(<HOUSEDESIGN>)
		RETURN 2
	ENDIF
	SYSMESSAGE @32,,1 You cannot do this whilst designing a house.
	RETURN 1

ON=@SkillUseQuick
	IF !(<HOUSEDESIGN>)
		RETURN 2
	ENDIF
	RETURN 1

// ON=@HouseDesignCommitItem
// serv.log <local.id> --- <local.p.x> ---- <local.p.y> ------ <local.p.z> ------- <local.visible>

ON=@HouseDesignCommit
	IF (<ISGM>)
		SYSMESSAGE @,,1 Your new house design has been committed.
		RETURN 2
	ENDIF
	LOCAL.OLDCOST = <EVAL (<ARGN1> * 500)>
	LOCAL.NEWCOST = <EVAL (<ARGN2> * 500)>
	LOCAL.CURCOST = <EVAL (<LOCAL.NEWCOST> - <LOCAL.OLDCOST>)>
	IF (<GOLD> < <LOCAL.CURCOST>)
		SYSMESSAGE @32,,1 You cannot afford this house design.
		RETURN 1
	ENDIF
	argo.tag0.construction=<eval <local.newcost>-<argo.value>>
	SYSMESSAGE @,,1 Your new house design has been committed.
	IF (<LOCAL.CURCOST> == 0)
		SYSMESSAGE @,,1 As the new design costs the same as the previous one, no gold has been taken out of your account.
	ELIF (<LOCAL.CURCOST> < 0)
		LOCAL.CURCOST = <EVAL (0 - <LOCAL.CURCOST>)>
		GOLD += <LOCAL.CURCOST>
		SYSMESSAGE @,,1 As the new design is cheaper than the previous one, <dLOCAL.CURCOST> gold has been returned to you.
	ELSE
		GOLD -= <LOCAL.CURCOST>
		SYSMESSAGE @,,1 <dLOCAL.CURCOST> gold has been taken out of your account to pay for the construction.
	ENDIF
	RETURN 2

ON=@HouseDesignExit
	SYSMESSAGE @,,1 You have left house design mode.
	events -e_house_customize
	if <argo.movingcrate>
		ref1=<argo.movingcrate>
		ref1.attr |= attr_invis
		ref1.p = <argo.p>
		ref1.move 0,0,-20
	endif
	RETURN 2

[EVENTS e_moving_crate]
ON=@logout
for 0 <eval <houses>-1>
	ref1=<house.<dlocal._for>.MovingCrate>
	if (<ref1>)
		ref1.attr |= attr_invis
		ref1.p <ref1.link.p>
		ref1.move 0 0 -20
	endif
endfor
events -e_moving_crate

ON=@HouseDesignBegin
for 0 <eval <houses>-1>
	ref1=<house.<dlocal._for>.MovingCrate>
	if (<ref1>)
		ref1.attr |= attr_invis
		ref1.p <ref1.link.p>
		ref1.move 0 0 -20
	endif
endfor
events -e_moving_crate

// Event applied when you are listed (with any privilege) on one or more houses.
[EVENTS e_house_priv]

// Event applied when you are listed (with any privilege) on one or more ships.
[EVENTS e_ship_priv]

[EOF]
