//***********************************************************************************
// SphereServer by: SphereServer development team and Menasoft.
// www.sphereServer.net
//***********************************************************************************
// Save in ANSI encoding
//***********************************************************************************
// New Multilanguage Admin
// Created by Golfin (http://eranova.cz/) and Jhobean (https://www.uocryptonite.com/)
//***********************************************************************************
// Last file update: 5.10.2020
//
VERSION=X

[DIALOG d_Search]
50,50
Src.Dialogclose d_SphereAdmin

Page 0 // Basic layout
Resizepic 0 0 5054 652 507
Gumppictiled 5 5 640 497 2604
Gumppictiled 5 5 640 25 2624
Gumppictiled 5 480 640 22 2624
Gumppictiled 5 35 640 440 2624
Checkertrans 5 5 640 497

DText 10 7 0c1 SphereServer <Src.Version> Admin Panel (<Serv.Servname>) - <Def.<Src.CTag0.AccountLang>_Search_Acc>
Button 615 5 4017 4018 1 0 0
DText 10 35 0c1 <Def.<Src.CTag0.AccountLang>_Search_Info>

Button 10 60 4005 4006 1 0 1
DText 45 60 2048 <Def.<Src.CTag0.AccountLang>_Account>
gumppictiled 120 62 175 18 87
Checkertrans 122 64 171 14
DTextentry 130 60 171 18 0480 1

Button 10 80 4005 4006 1 0 2
DText 45 80 2048 <Def.<Src.CTag0.AccountLang>_Character>
gumppictiled 120 82 175 18 87
Checkertrans 122 84 171 14
DTextentry 130 80 171 18 0480 2

Button 10 100 4005 4006 1 0 3
DText 45 100 2048 <Def.<Src.CTag0.AccountLang>_IP_Address>
gumppictiled 120 102 175 18 87
Checkertrans 122 104 171 14
DTextentry 130 100 171 18 0480 3

Button 10 120 4005 4006 1 0 4
DText 45 120 2048 <Def.<Src.CTag0.AccountLang>_Location>
gumppictiled 120 122 175 18 87
Checkertrans 122 124 171 14
DTextentry 130 120 171 18 0480 4

dtext 340 60 193 <Def.<Src.CTag0.AccountLang>_Filter>
radio 340 80 210 211 1 1
dtext 365 80 2048 <Def.<Src.CTag0.AccountLang>_Online>
radio 340 100 210 211 0 2
dtext 365 100 2048 <Def.<Src.CTag0.AccountLang>_Offline>
radio 340 120 210 211 0 3
dtext 365 120 2048 <Def.<Src.CTag0.AccountLang>_Both>

[DIALOG d_Search Button]

On=0
   Dialog d_SphereAdmin

On=1 4
   If (<Account.Plevel> >= <Def0.Admin_ReqPlevelSearch>)
      ClearCTags Dialog.Admin
      If (<ArgCHK[1]>)
         For 0 <Eval <Serv.Clients>-1>
            Ref1=<Serv.Client.<Local._FOR>.UID>
            If (<Ref1>)
               If (<Hval STRMATCH(<ArgTxt[<ArgN>]>*,<Ref1.<Def0.Admin.SearchType.<ArgN>>>*)>)
                  Ref1.f_Admin_GetPlayers
               Endif
            Endif
         Endfor
      Else
         For 0 <Eval <Serv.Accounts>-1>
            For n 0 <Eval <Serv.Account.<Local._For>.Chars>-1>
               Ref1=<Serv.Account.<Local._For>.Char.<Local.N>>
               If (<Ref1>)
                  If (((<ArgCHK[2]>) && !(<Ref1.isOnline>)) || (<ArgCHK[3]>))
                     If (<Hval STRMATCH(<ArgTxt[<ArgN>]>*,<Ref1.<Def0.Admin.SearchType.<ArgN>>>*)>)
                        Ref1.f_Admin_GetPlayers
                     Endif
                  Endif
               Endif
            Endfor
         Endfor
      Endif
      CTag.Dialog.Admin.Page=1
      CTag.Dialog.Admin.MaxPage=<Eval (((<CTag0.Dialog.Admin.Clients>-1)/15)+1)>
      CTag.Dialog.Admin.Search=1
   Endif
   DIALOG d_SphereAdmin

[DefNAME AdminSearchType]
Admin.SearchType.1	Account
Admin.SearchType.2	Name
Admin.SearchType.3	Account.LastIP
Admin.SearchType.4	Region.Name

[EOF]